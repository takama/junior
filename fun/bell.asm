.model small
.code
        org     100h                            ;загрузить с адреса 100h
start:                                          ;точка входа
        jmp     inst                            ;переход на инсталяцию
;----------------------------------------------------------------------------
;------ обработка прерывания 8h ---------------------------------------------
;----------------------------------------------------------------------------
int8    label   byte                            ;точка входа
        cli                                     ;запрет прерываний
        push    ax                              ;сохранить в стеке ax и ds
        push    ds                              ;
        pushf                                   ;сохранить флаги
        db      9ah                             ;команда вызова программы,
old8o   dw      0                               ;адрес которой находится
old8s   dw      0                               ;ниже
        xor     ax,ax                           ;ax=0
        mov     ds,ax                           ;ds=0
        cmp     word ptr ds:[46ch],0ff00h       ;сравнить ячейку таймера
        jnz     nosound                         ;если нет, то переход
        cli                                     ;запрет прерываний
        mov     ax,word ptr cs:old8o            ;установка старого вектора
        mov     word ptr ds:[20h],ax            ;прерываний 8h в таблицу
        mov     ax,word ptr cs:old8s            ;векторов
        mov     word ptr ds:[22h],ax            ;
        sti                                     ;разрешение прерываний
        call    melody                          ;вызов подпрограммы мелодии
        cli                                     ;запрет прерываний
        push    cs                              ;установка нового вектора
        pop     word ptr ds:[22h]               ;прерываний 8h в таблицу
        mov     word ptr ds:[20h],offset int8   ;векторов
        sti                                     ;разрешение прерываний
nosound:                                        ;
        pop     ds                              ;вытолкнуть из стека
        pop     ax                              ;ds и ax
        iret                                    ;возврат из прерывания
;----------------------------------------------------------------------------
;------ подпрограмма проигрывания мелодии -----------------------------------
;----------------------------------------------------------------------------
melody  proc    near                            ;
        push    bx                              ;сохранить регистры
        push    dx                              ;bx, dx, si, ds
        push    si                              ;
        push    ds                              ;
        push    cs                              ;
        pop     ds                              ;ds = cs
        in      al,61h                          ;чтение порта 61h
        or      al,00000011b                    ;установка двух младших битов
        out     61h,al                          ;запись в порт 61h значения
        mov     si,offset freq                  ;в si смещение мелодии
        mov     al,0b6h                         ;инициализация канала 2
        out     43h,al                          ;таймера

next_n:                                         ;
        lodsw                                   ;загрузить частоту ноты в ax
        cmp     al,0ffh                         ;сравнить с концом мелодии
        je      no_more                         ;если да, то переход
        out     42h,al                          ;младший байт частоты
        mov     al,ah                           ;
        out     42h,al                          ;старший байт частоты
        mov     ah,0                            ;
        int     1ah                             ;получить значение счетчика
        lodsb                                   ;длину очередной ноты в al
        mov     bx,dx                           ;младшее слово счетчика
        add     bx,ax                           ;определяем момент окончания
still_s:                                        ;
        mov     ah,0                            ;
        int     1ah                             ;получить значение счетчика
        cmp     dx,bx                           ;если не равно то
        jne     still_s                         ;переход (ожидание)
        jmp     short next_n                    ;загрузка следующей ноты
no_more:                                        ;
        in      al,61h                          ;выключить динамик
        and     al,0fch                         ;
        out     61h,al                          ;
        pop     ds                              ;вытолкнуть из стека
        pop     si                              ;ds, si, dx, bx
        pop     dx                              ;
        pop     bx                              ;
        ret                                     ;возврат из подпрограммы
melody  endp                                    ;конец подпрограммы
;----------------------------------------------------------------------------
;------ блок данных для проигрывания мелодии --------------------------------
;----------------------------------------------------------------------------
freq    db      23h,0eh,9,0e3h,0bh,9,6fh,9,9,0e3h,0bh,9,97h,0ah,18
        db      0e3h,0bh,9,98h,0ch,9,6fh,9,18,97h,0ah,18,23h,0eh,18
        db      54h,0,18,0e8h,8,3,54h,0,15,0e8h,8,3,54h,0,15,0e8h,8,3
        db      54h,0,15,0e8h,8,3,54h,0,15,0e8h,8,3,54h,0,15,0e8h,8,9,0ffh
;----------------------------------------------------------------------------
inst:                                           ;инициализация
        mov     ax,3508h                        ;занести вектор прерывания
        int     21h                             ;8h в подготовленную ячейку
        mov     word ptr [old8o],bx             ;памяти
        mov     word ptr [old8s],es             ;
        cli                                     ;запрет прерываний
        lea     dx,int8                         ;загрузить новый обработчик
        mov     ax,2508h                        ;прерывания
        int     21h                             ;
        sti                                     ;разрешение прерываний
        lea     dx,inst                         ;адрес конца программы
        int     27h                             ;выйти в ДОС оставив в
        end     start                           ;резиденте обработчик
                                                ;прерывания 8h

